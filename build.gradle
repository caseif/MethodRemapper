plugins {
    id 'java'
    id 'maven-publish'

    id 'eclipse'
    id 'idea'

    id 'org.cadixdev.licenser' version '0.6.1'
}

version = '0.3-SNAPSHOT'
description = 'A Gradle plugin with Java API to remap method names during the build'

ext {
    github = 'caseif/MethodRemapper'
    url = "https://github.com/$github"

    projectName = 'MethodRemapper'
    artifactId = 'remapper'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    apply plugin: 'eclipse'
    apply plugin: 'idea'

    apply plugin: 'org.cadixdev.licenser'
}

allprojects {
    group = 'net.caseif.methodremapper'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    repositories {
        mavenCentral()
    }

    license {
        header = rootProject.file('LICENSE')
        include '**/*.java'
        include '**/*.groovy'
    }

    task sourceJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    afterEvaluate {
        publishing {
            publications {
                mavenJava(MavenPublication) {
                    from components.java

                    if (project.tasks.findByName('javadocJar')) {
                        artifact javadocJar
                    }

                    artifact sourceJar

                    artifactId project.artifactId

                    pom.withXml {
                        asNode().children().last() + {
                            // Why is this even necessary?
                            resolveStrategy = Closure.DELEGATE_FIRST

                            name project.projectName
                            description project.description

                            organization {
                                name 'Lapis'
                                url 'https://lapis.blue'
                            }

                            licenses {
                                license {
                                    name 'MIT License'
                                    url 'http://opensource.org/licenses/MIT'
                                    distribution 'repo'
                                }
                            }

                            scm {
                                url project.url
                                connection "scm:git:${project.url}.git"
                                developerConnection "scm:git:git@github.com:${github}.git"
                            }

                            issueManagement {
                                system 'github'
                                url "$project.url/issues"
                            }
                        }
                    }

                    if (!version.endsWith('-SNAPSHOT')) {
                        repositories {
                            maven {
                                url = System.getenv('REPO_RELEASES') ?: "$buildDir/repo"
                            }
                        }
                    }

                }
            }
        }
    }
}

subprojects {
    dependencies {
        implementation rootProject
        implementation 'com.google.guava:guava:17.0'
    }
}

dependencies {
    implementation 'com.google.guava:guava:17.0'
    implementation 'org.ow2.asm:asm:5.0.3'
    implementation 'org.slf4j:slf4j-api:1.7.12'
}

javadoc {
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
    options.links(
            'http://www.slf4j.org/apidocs/',
            'https://guava.dev/releases/17.0/api/docs/',
            'http://asm.ow2.org/asm50/javadoc/user/',
            'http://docs.oracle.com/javase/8/docs/api/'
    )
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

wrapper {
    gradleVersion = '7.4.1'
}
